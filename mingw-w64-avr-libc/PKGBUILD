# Maintainer: fauxpark <fauxpark@gmail.com>

_realname=avr-libc
pkgbase=mingw-w64-${_realname}
pkgname=${MINGW_PACKAGE_PREFIX}-${_realname}
pkgver=2.0.0
pkgrel=1
pkgdesc='The C runtime library for the AVR family of microcontrollers (mingw-w64)'
arch=('any')
license=('BSD')
url='https://savannah.nongnu.org/projects/avr-libc/'
options=(!strip)
depends=("${MINGW_PACKAGE_PREFIX}-avr-gcc")
source=("https://download.savannah.gnu.org/releases/avr-libc/avr-libc-${pkgver}.tar.bz2")
sha256sums=('b2dd7fd2eefd8d8646ef6a325f6f0665537e2f604ed02828ced748d49dc85b97')

prepare() {
    cd ${srcdir}/avr-libc-${pkgver}

    mkdir avr-libc-build
}

build() {
    cd ${srcdir}/avr-libc-${pkgver}/avr-libc-build

    ../configure \
        --build=${MINGW_CHOST} \
        --host=avr \
        --prefix=${MINGW_PREFIX}
    make
}

package() {
    cd ${srcdir}/${_realname}-${pkgver}/avr-libc-build

    make DESTDIR="$pkgdir" install
}
