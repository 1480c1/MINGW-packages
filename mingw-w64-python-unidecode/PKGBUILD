# Maintainer: Sarah Ottinger

_realname=unidecode
pkgbase=mingw-w64-python-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-python-${_realname}")
provides=("${MINGW_PACKAGE_PREFIX}-python3-${_realname}")
conflicts=("${MINGW_PACKAGE_PREFIX}-python3-${_realname}")
replaces=("${MINGW_PACKAGE_PREFIX}-python3-${_realname}")
pkgver=1.1.1
pkgrel=1
pkgdesc='ASCII transliterations of Unicode text (mingw-w64)'
arch=('any')
url="https://github.com/avian2/unidecode"
license=('GPL')
depends=("${MINGW_PACKAGE_PREFIX}-python")
makedepends=("git" "${MINGW_PACKAGE_PREFIX}-python-setuptools")
source=(git+https://github.com/avian2/unidecode.git#tag=unidecode-${pkgver})
sha256sums=(SKIP)

prepare() {  
  cd "$srcdir"
  rm -rf python-build-${CARCH} | true
  cp -r "${_realname}" "python-build-${CARCH}"
}

build() {
  msg "Python build for ${CARCH}"  
  cd "${srcdir}/python-build-${CARCH}"
  ${MINGW_PREFIX}/bin/python setup.py build
}

package() {
  cd "${srcdir}/python-build-${CARCH}"
  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
  ${MINGW_PREFIX}/bin/python setup.py install --prefix=${MINGW_PREFIX} \
    --root="${pkgdir}" --optimize=1 --skip-build
}
