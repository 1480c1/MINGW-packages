# Maintainer: @naveen521kk on Github, Naveen M K <naveen@syrusdark.website>

_realname=texlive-latexextra
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=2021.20210424
_revnr=${pkgver#2021.}
pkgrel=1
pkgdesc="TeX Live - Large collection of add-on packages for LaTeX (mingw-w64)"
license=('GPL')
arch=('any')
depends=(
   "${MINGW_PACKAGE_PREFIX}-texlive-core"
)
optdepends=(
   "${MINGW_PACKAGE_PREFIX}-python-pygments: for pygmentex"
   #"${MINGW_PACKAGE_PREFIX}-texlive-genericextra: to use the calctab package"
   #"${MINGW_PACKAGE_PREFIX}-texlive-pictures: to use the package overpic"
   #"${MINGW_PACKAGE_PREFIX}-java-environment: to use pdfannotextractor"
)
groups=("${MINGW_PACKAGE_PREFIX}-texlive-most")
url='http://tug.org/texlive/'
source=("https://github.com/msys2/msys2-texlive/releases/download/${_revnr}/${_realname}-${_revnr}.tar.xz"
   "$_realname.maps::https://github.com/msys2/msys2-texlive/releases/download/${_revnr}/${_realname}.maps"
   "$_realname.fmts::https://github.com/msys2/msys2-texlive/releases/download/${_revnr}/${_realname}.fmts"
   "$_realname.dat::https://github.com/msys2/msys2-texlive/releases/download/${_revnr}/${_realname}.dat"
   "$_realname.dat.lua::https://github.com/msys2/msys2-texlive/releases/download/${_revnr}/${_realname}.dat.lua"
   "$_realname.def::https://github.com/msys2/msys2-texlive/releases/download/${_revnr}/${_realname}.def")
options=('!emptydirs')
sha256sums=('aec98e10b7888715c03ac883100ee831e029fd4d01f4fcd0b77c33650582c61f'
   'ff58b229cb23d1f0aab38b2e4060fbeaff7e6564e042c9763a1dbb3fc136d24a'
   'b819763ed32a2bac65c1d703c6e8ac27d7a42903acd0c810995c693f65427192'
   'daffefdb7335ff880ee9f2ceec60bbad33ee7d3b65c4b9c4a06bdd9c146658f8'
   '081be3efa35fc341575ece505756cce0210cb343a67a72ca5f0fd9f4d6ce4884'
   'daffefdb7335ff880ee9f2ceec60bbad33ee7d3b65c4b9c4a06bdd9c146658f8')

build() {
   for p in *.tar.xz; do
      bsdtar -xf $p
   done
   rm -rf {tlpkg,doc,source} || true
}

package() {
   msg "Installing Package"

   # Install packages.
   install -m755 -d "${pkgdir}${MINGW_PREFIX}/var/lib/texmf/msys2/installedpkgs"
   sed -i '/^#/d' CONTENTS
   install -m644 CONTENTS "${pkgdir}${MINGW_PREFIX}/var/lib/texmf/msys2/installedpkgs/${pkgname}_${_revnr}.pkgs"
   install -m644 $_realname.maps "${pkgdir}${MINGW_PREFIX}/var/lib/texmf/msys2/installedpkgs/"
   install -m644 $_realname.fmts "${pkgdir}${MINGW_PREFIX}/var/lib/texmf/msys2/installedpkgs/"

   # language files for hooks
   sed -i 's/\% test//' $_realname.dat
   install -m644 $_realname.dat "${pkgdir}${MINGW_PREFIX}/var/lib/texmf/msys2/installedpkgs/"

   sed -i 's/-- test//' $_realname.dat.lua
   install -m644 $_realname.dat.lua "${pkgdir}${MINGW_PREFIX}/var/lib/texmf/msys2/installedpkgs/"

   sed -i 's/\% test//' $_realname.def
   install -m644 $_realname.def "${pkgdir}${MINGW_PREFIX}/var/lib/texmf/msys2/installedpkgs/"

   install -m755 -d "${pkgdir}${MINGW_PREFIX}/share"
   wanteddirs=$(for d in *; do test -d $d && [[ $d != texmf* ]] && echo $d; done) || true
   for dir in $wanteddirs; do
      find $dir -type d -exec install -d -m755 "${pkgdir}${MINGW_PREFIX}"/share/texmf-dist/'{}' \;
      find $dir -type f -exec install -m644 '{}' "${pkgdir}${MINGW_PREFIX}"/share/texmf-dist/'{}' \;
   done

   if [[ -d texmf-dist ]]; then
      find texmf-dist -type d -exec install -d -m755 "${pkgdir}${MINGW_PREFIX}"/share/'{}' \;
      find texmf-dist -type f -exec install -m644 '{}' "${pkgdir}${MINGW_PREFIX}"/share/'{}' \;
   fi
   if [[ -d "$pkgdir"/share/texmf-dist/scripts ]]; then
      find "${pkgdir}${MINGW_PREFIX}"/share/texmf-dist/scripts -type f -exec chmod a+x '{}' \;
   fi

   msg "Install Scripts"
   #add lauchers:
   _linked_scripts="
authorindex/authorindex
exceltex/exceltex
glossaries/makeglossaries
glossaries/makeglossaries-lite.lua
hyperxmp/hyperxmp-add-bytecount.pl
l3build/l3build.lua
makedtx/makedtx.pl
pax/pdfannotextractor.pl
perltex/perltex.pl
pygmentex/pygmentex.py
splitindex/splitindex.pl
svn-multi/svn-multi.pl
vpe/vpe.pl
webquiz/webquiz.py
wordcount/wordcount.sh
yplan/yplan
"
   install -m755 -d "${pkgdir}${MINGW_PREFIX}"/bin
   for _script in ${_linked_scripts}; do
      _scriptbase=$(basename $_script)
      _scriptbase=${_scriptbase%.*}
      install -D -m755 ${MINGW_PREFIX}/bin/runscript.exe "${pkgdir}${MINGW_PREFIX}/bin/${_scriptbase}.exe"
   done
}
