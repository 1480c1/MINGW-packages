_realname=libwebm
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=1.0.0.27
pkgrel=1
pkgdesc="WebM video file parser (mingw-w64)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64')
url="https://chromium.googlesource.com/webm/libwebm"
license=('BSD')
makedepends=("${MINGW_PACKAGE_PREFIX}-cmake")
source=(https://github.com/webmproject/${_realname}/archive/refs/tags/${_realname}-${pkgver}.tar.gz
        msys2_changes.patch
		missing_includes.patch)
sha256sums=('1332f43742aeae215fd8df1be6e363e753b17abb37447190e789299fe3edec77'
            '775128adfaa22ec4c764f1408ee8583e8abed824c1fef7dd07e0b5478b4952a1'
			'05a78fbba1d55d7d6cd10961841dd130111d8efb380bb30ed33f78335a0d4193')

prepare() {
  cd ${srcdir}/${_realname}-${_realname}-${pkgver}
  patch -p1 -i ${srcdir}/msys2_changes.patch
  patch -p1 -i ${srcdir}/missing_includes.patch
}

build() {
  [[ -d ${srcdir}/build-${MINGW_CHOST} ]] && rm -rf ${srcdir}/build-${MINGW_CHOST}
  
  mkdir -p ${srcdir}/build-${MINGW_CHOST} && cd ${srcdir}/build-${MINGW_CHOST}
  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
  cmake -G"MSYS Makefiles" \
        -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
		-DCMAKE_BUILD_TYPE=Release \
		../${_realname}-${_realname}-${pkgver}
  make
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make install DESTDIR="${pkgdir}"
  cd ${srcdir}/${_realname}-${_realname}-${pkgver}
  install -Dm644 LICENSE.txt "$pkgdir${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE"
}
