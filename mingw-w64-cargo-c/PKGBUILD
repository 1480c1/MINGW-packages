# Maintainer: Christoph Reiter <reiter.christoph@gmail.com>

_realname=cargo-c
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=0.6.8
pkgrel=1
pkgdesc='A cargo subcommand to build and install C-ABI compatibile dynamic and static libraries (mingw-w64)'
arch=('any')
url='https://github.com/lu-zero/cargo-c/'
license=('MIT')
depends=(
    "${MINGW_PACKAGE_PREFIX}-curl"
    "${MINGW_PACKAGE_PREFIX}-openssl"
    "${MINGW_PACKAGE_PREFIX}-zlib")
makedepends=(
    "${MINGW_PACKAGE_PREFIX}-rust")
source=("${_realname}-${pkgver}.tar.gz"::"https://github.com/lu-zero/cargo-c/archive/v${pkgver}.tar.gz"
        "${_realname}-${pkgver}.Cargo.lock"::"https://github.com/lu-zero/cargo-c/releases/download/v${pkgver}/Cargo.lock"
        "fix-append_to_destdir.patch")
sha256sums=('ca8cb5e08b0ba5b6eccea1481854829e1b411ebb9885be891a897c27b5a76cba'
            'f956c171676375ecdf5069d32896c8c8047001b207f377d1ba6442be449a3688'
            '474ca4a7504d563f76455c13cedeb04cef0b665fa5d9c596ddd141393fb7079c')

prepare() {
    cp "${srcdir}/${_realname}-${pkgver}.Cargo.lock" "${_realname}-${pkgver}/Cargo.lock"
    cd "${srcdir}/${_realname}-${pkgver}"

    # https://github.com/lu-zero/cargo-c/pull/90
    patch -Np1 -i "${srcdir}"/fix-append_to_destdir.patch

    cargo fetch \
        --locked
}

build() {
    cd "${srcdir}/${_realname}-${pkgver}"

    cargo build \
        --release \
        --frozen \
        --offline
}

check() {
    cd "${srcdir}/${_realname}-${pkgver}"

    cargo test \
        --release \
        --frozen
}

package() {
    cd "${srcdir}/${_realname}-${pkgver}"

    cargo install \
    --frozen \
    --offline \
    --no-track \
    --path . \
    --root "${pkgdir}${MINGW_PREFIX}"

    install -D -m644 "LICENSE" -t "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}"
}
