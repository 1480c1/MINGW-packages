# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=zziplib
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=0.13.70
pkgrel=1
pkgdesc="A lightweight library that offers the ability to easily extract data from files archived in a single zip file (mingw-w64)"
arch=('any')
url="https://zziplib.sourceforge.io/"
license=("LGPL, MPL")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc"
             "${MINGW_PACKAGE_PREFIX}-pkg-config"
             "${MINGW_PACKAGE_PREFIX}-SDL"
             "${MINGW_PACKAGE_PREFIX}-python")
depends=("${MINGW_PACKAGE_PREFIX}-zlib")
optdepends=("${MINGW_PACKAGE_PREFIX}-SDL: SDL_rwops for ZZipLib")
options=('staticlibs' 'strip')
source=("${_realname}-${pkgver}.tar.gz"::"https://github.com/gdraheim/zziplib/archive/v${pkgver}.tar.gz"
        "configure.patch"
        "fix-upstream-merge-hunk.patch")
sha256sums=('a1457262d7a237dc50ce1f98ca57242bc714055ff81146f419ee53cdea1bf029'
            '892162a8535575891663607b8fb5e3be518e8293e9b0f22cf437b1d83a58b008'
            'ef6da465673997df0fa08789a1e7d9e3809a543bd0d033c3b0bf7b4ccd662fbf')

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"
  patch -Np1 -i ${srcdir}/configure.patch
  patch -Np1 -i ${srcdir}/fix-upstream-merge-hunk.patch

  autoreconf -fiv
}

build() {
  export PYTHON=${MINGW_PREFIX}/bin/python
  export PERL=/usr/bin/perl
  mkdir -p "${srcdir}/build-${MINGW_CHOST}"
  cd "${srcdir}/build-${MINGW_CHOST}"
  ../${_realname}-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --with-zlib=${MINGW_PREFIX} \
    --enable-sdl

  make
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make DESTDIR="${pkgdir}" install
}
