# Maintainer: Ray Donnelly <mingwandroid@gmail.com>
# Upstream Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Upstream Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>

_realname=frei0r-plugins
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=1.7.0
pkgrel=0
pkgdesc="Minimalistic plugin API for video effects"
arch=('any')
url="https://frei0r.dyne.org/"
license=('GPL')
depends=("${MINGW_PACKAGE_PREFIX}-cairo"
         "${MINGW_PACKAGE_PREFIX}-gavl"
         "${MINGW_PACKAGE_PREFIX}-opencv")
makedepends=("${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-doxygen")
source=("https://files.dyne.org/frei0r/$_realname-$pkgver.tar.gz"
        "https://files.dyne.org/frei0r/$_realname-$pkgver.tar.gz.asc"
        "0001-Fix-autotools-build-for-opencv4.patch"
        "0002-Fix-out-of-source-tree-CXX-build-failures.patch"
        "0003-Support-MSYS2-mingw-w64.patch")
validpgpkeys=('6113D89CA825C5CEDD02C87273B35DA54ACB7D10') # Denis Roio (Jaromil)
sha256sums=('1b1ff8f0f9bc23eed724e94e9a7c1d8f0244bfe33424bb4fe68e6460c088523a'
            'SKIP'
            '24060fc9721348e4261eeba3e4f8e39bbf8695bbff45da06fca566d539a2bf78'
            '363ed95d4460045ed1803eab80b052b3cea10b1df34ab85a29975a973066143c'
            '0328840c10082380e3a9bcf58e0ae373d45f54fa3cfd7392a58984e1b6c89ba3')


# =========================================== #
#   Helper macros to help make tasks easier   #
apply_patch_with_msg() {
  for _patch in "$@"
  do
    msg2 "Applying ${_patch}"
    patch -Nbp1 -i "${srcdir}/${_patch}"
  done
}

del_file_exists() {
  for _fname in "$@"
  do
    if [ -f ${_fname} ]; then
      rm -rf ${_fname}
    fi
  done
}
# =========================================== #

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"

  plain "Autotools with OpenCV4 fix (upstreamed already)"
  apply_patch_with_msg \
    0001-Fix-autotools-build-for-opencv4.patch

  plain "General autotools fix (upstreaming .. hopefully)"
  apply_patch_with_msg \
    0002-Fix-out-of-source-tree-CXX-build-failures.patch

  plain "MSYS2/mingw-w64 autotools fixes (upstreaming .. hopefully)"
  apply_patch_with_msg \
    0003-Support-MSYS2-mingw-w64.patch

  # The 1.7.0 tarball seems to have incorrectly named files:
  cp AUTHORS.txt AUTHORS
  cp ChangeLog.txt ChangeLog
  cp COPYING.txt COPYING
  cp README.txt README.md
  cp INSTALL.txt INSTALL.md
  cp TODO.txt TODO
}

build() {
  mkdir -p "${srcdir}/build-${MINGW_CHOST}"
  cd "${srcdir}/build-${MINGW_CHOST}"

  ../${_realname}-${pkgver}/configure  \
    --prefix=${MINGW_PREFIX}

  plain "Start building..."
  make
}

check() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make check
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make DESTDIR="${pkgdir}" install
}
