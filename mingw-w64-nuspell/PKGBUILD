_realname=nuspell
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=3.1.2
pkgrel=1
pkgdesc="Fast and safe spellchecking C++ library (mingw-w64)"
arch=('any')
url="https://nuspell.github.io/"
license=(LGPL3+)
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc"
             "${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-catch"
             "${MINGW_PACKAGE_PREFIX}-ruby-ronn")
depends=("${MINGW_PACKAGE_PREFIX}-icu"
         "${MINGW_PACKAGE_PREFIX}-boost")
source=("${_realname}-${pkgver}.tar.gz"::"https://github.com/nuspell/nuspell/archive/v${pkgver}.tar.gz")
sha256sums=('f59f8a27e97047f30659182f244daca8e05e052710c0ea5d845c2cb00f6c6eca')
sha512sums=('ed5206cb9ffa7e4ec6843988a75627d4ae7d5170444b6e7bd3496c1bf61c25412c4e68864800f2ad238d014b4e50d25282f367de2900f8a230f3ecc9ae5772be')

prepare() {
  # https://github.com/nuspell/nuspell/issues/71
  # once fixed upstream, delete this patching
  cd "${srcdir}/${_realname}-${pkgver}"
  sed -i 's/-I@Boost_INCLUDE_DIRS@//' nuspell.pc.in
}

build() {
  local build_dir="${srcdir}/build-${MINGW_CHOST}"
  [[ -d "$build_dir" ]] && rm -rf "$build_dir"
  mkdir "$build_dir" && cd "$build_dir"
  
  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
  ${MINGW_PREFIX}/bin/cmake.exe ../"${_realname}-${pkgver}" \
    -G "MSYS Makefiles" \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX="${MINGW_PREFIX}" \
    -DBUILD_SHARED_LIBS=ON

  make
}

check() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  ctest
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make DESTDIR="$pkgdir" install

  # Licenses
  cd "${srcdir}/${_realname}-${pkgver}"
  install -Dm644 COPYING      "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/COPYING"
  install -Dm644 COPYING.LESSER "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/COPYING.LESSER"
}
