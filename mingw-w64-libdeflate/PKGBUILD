# Maintainer: Miloš Komarčević <miloskomarcevic@aim.com>

_realname=libdeflate
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=1.6
pkgrel=1
pkgdesc="Heavily optimized library for DEFLATE/zlib/gzip compression and decompression (mingw-w64)"
arch=('any')
url='https://github.com/ebiggers/libdeflate'
license=('MIT')
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc")
source=(${_realname}-${pkgver}.tar.gz::"https://github.com/ebiggers/libdeflate/archive/v${pkgver}.tar.gz"
        "001-libdeflate-makefile-mingw-fix.patch")
sha256sums=('60748f3f7b22dae846bc489b22a4f1b75eab052bf403dd8e16c8279f16f5171e'
            '821eba50ce2bef4d993a1faa2b0b3c3cdbd6a77a792557290e642c750e358468')

prepare() {
  cd "${srcdir}"/${_realname}-${pkgver}
  patch -p1 -i "${srcdir}"/001-libdeflate-makefile-mingw-fix.patch
}

build() {
  [[ -d "${srcdir}"/build-${CARCH} ]] && rm -rf "${srcdir}"/build-${CARCH}
  cp -a "${srcdir}"/${_realname}-${pkgver} "${srcdir}"/build-${CARCH} && cd "${srcdir}"/build-${CARCH}

  make PREFIX=${MINGW_PREFIX}
}

package() {
  cd "${srcdir}"/build-${CARCH}
  make install PREFIX=${MINGW_PREFIX} DESTDIR="${pkgdir}"

  install -Dm644 "${srcdir}"/${_realname}-${pkgver}/COPYING "${pkgdir}"${MINGW_PREFIX}/share/licenses/${_realname}/COPYING
}
