# Maintainer: Rafal Brzegowy <rafal.brzegowy@yahoo.com>

_realname=lis
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=2.0.23
pkgrel=1
pkgdesc='Library of Iterative Solvers for linear systems (mingw-w64)'
arch=('any')
depends=("${MINGW_PACKAGE_PREFIX}-libtool")
options=('!docs')
license=('BSD')
url='https://www.ssisc.org/lis/'
source=(https://www.ssisc.org/lis/dl/${_realname}-${pkgver}.zip)
sha512sums=('A6C8596FA61435A5577CB63444B33808161FE21402FB1F4AE179DEFB2817E6E9036FBE6F8014EADC96E97C16D2764558019A31D2B53BB54E029C579592801E7C')

prepare() {
  cd ${_realname}-${pkgver}
  autoreconf -fiv
}

build() {
  [[ -d "${srcdir}/build-${MINGW_CHOST}" ]] && rm -rf "${srcdir}/build-${MINGW_CHOST}"
  mkdir -p "${srcdir}/build-${MINGW_CHOST}" && cd "${srcdir}/build-${MINGW_CHOST}"

  ../${_realname}-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --enable-fortran \
    --enable-f90 \
    --enable-omp \
    --with-gnu-ld

  make
}

check() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make check || true
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make install DESTDIR="${pkgdir}"
  install -Dm644 ${srcdir}/${_realname}-${pkgver}/COPYING ${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE
}

# vim: ts=2 sw=2 et:
