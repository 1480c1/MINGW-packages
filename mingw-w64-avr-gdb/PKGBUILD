# Maintainer: fauxpark <fauxpark@gmail.com>

_realname=avr-gdb
pkgbase=mingw-w64-${_realname}
pkgname=${MINGW_PACKAGE_PREFIX}-${_realname}
pkgver=9.2
pkgrel=1
pkgdesc='The GNU Debugger for AVR (mingw-w64)'
arch=('any')
license=('GPL')
url='https://www.gnu.org/software/gdb/'
source=("https://ftp.gnu.org/gnu/gdb/gdb-${pkgver}.tar.xz")
sha256sums=('360cd7ae79b776988e89d8f9a01c985d0b1fa21c767a4295e5f88cb49175c555')

prepare() {
    cd ${srcdir}/gdb-${pkgver}

    mkdir gdb-build
}

build() {
    cd ${srcdir}/gdb-${pkgver}/gdb-build

    ../configure \
        --build=${MINGW_CHOST} \
        --prefix=${MINGW_PREFIX} \
        --target=avr \
        --disable-nls \
        --disable-debug \
        --disable-dependency-tracking \
        --disable-binutils \
        --disable-libssp \
        --disable-install-libbfd \
        --disable-install-libiberty \
        --with-system-readline
    make
}

package() {
    cd ${srcdir}/gdb-${pkgver}/gdb-build

    make DESTDIR="$pkgdir" install-gdb

    cd ${pkgdir}${MINGW_PREFIX}

    # Remove files that conflict with native gdb
    rm -rf include share/gdb share/info
}
