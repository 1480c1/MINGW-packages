# Maintainer: @naveen521kk on Github, Naveen M K <naveen@syrusdark.website>

_realname=texlive-latex-extra
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=2021.20210501
_revnr=${pkgver#2021.}
pkgrel=1
pkgdesc="TeX Live - A very large collection of add-on packages for LaTeX. (mingw-w64)"
license=('GPL')
arch=('any')
depends=(
   "${MINGW_PACKAGE_PREFIX}-texlive-core"
   "${MINGW_PACKAGE_PREFIX}-texlive-latex-recommended"
   "${MINGW_PACKAGE_PREFIX}-texlive-pictures"
)
optdepends=(
   "${MINGW_PACKAGE_PREFIX}-python-pygments: for pygmentex"
   #"${MINGW_PACKAGE_PREFIX}-texlive-genericextra: to use the calctab package"
   #"${MINGW_PACKAGE_PREFIX}-java-environment: to use pdfannotextractor"
)
groups=("${MINGW_PACKAGE_PREFIX}-texlive-most")
url='http://tug.org/texlive/'
source=("${_realname}-${_revnr}.tar.xz::https://github.com/msys2/msys2-texlive/releases/download/${_revnr}/${_realname}-${_revnr}.tar.xz"
        "${_realname}-extra-files.tar.xz::https://github.com/msys2/msys2-texlive/releases/download/${_revnr}/${_realname}-extra-files.tar.xz")
options=('!emptydirs')
sha256sums=('14d831e8e26d976373c7117e1ad81b1df348820817f802197dd5fd0cb759ec34'
            '7c7b1697b2a5e0c41e0cde8d12a5d8554634831c9e0fc587da549b0ccfc1885a')

build() {
   for p in *.tar.xz; do
      bsdtar -xf $p
   done
   rm -rf {tlpkg,doc,source} || true
}

package() {
   msg "Installing Package"

   # Install packages.
   install -m755 -d "${pkgdir}${MINGW_PREFIX}/var/lib/texmf/msys2/installedpkgs"
   sed -i '/^#/d' CONTENTS
   install -m644 CONTENTS "${pkgdir}${MINGW_PREFIX}/var/lib/texmf/msys2/installedpkgs/${pkgname}_${_revnr}.pkgs"
   install -m644 $_realname.maps "${pkgdir}${MINGW_PREFIX}/var/lib/texmf/msys2/installedpkgs/"
   install -m644 $_realname.fmts "${pkgdir}${MINGW_PREFIX}/var/lib/texmf/msys2/installedpkgs/"

   # language files for hooks
   sed -i 's/\% test//' $_realname.dat
   install -m644 $_realname.dat "${pkgdir}${MINGW_PREFIX}/var/lib/texmf/msys2/installedpkgs/"

   sed -i 's/-- test//' $_realname.dat.lua
   install -m644 $_realname.dat.lua "${pkgdir}${MINGW_PREFIX}/var/lib/texmf/msys2/installedpkgs/"

   sed -i 's/\% test//' $_realname.def
   install -m644 $_realname.def "${pkgdir}${MINGW_PREFIX}/var/lib/texmf/msys2/installedpkgs/"

   install -m755 -d "${pkgdir}${MINGW_PREFIX}/share"
   wanteddirs=$(for d in *; do test -d $d && [[ $d != texmf* ]] && echo $d; done) || true
   for dir in $wanteddirs; do
      find $dir -type d -exec install -d -m755 "${pkgdir}${MINGW_PREFIX}"/share/texmf-dist/'{}' \;
      find $dir -type f -exec install -m644 '{}' "${pkgdir}${MINGW_PREFIX}"/share/texmf-dist/'{}' \;
   done

   if [[ -d texmf-dist ]]; then
      find texmf-dist -type d -exec install -d -m755 "${pkgdir}${MINGW_PREFIX}"/share/'{}' \;
      find texmf-dist -type f -exec install -m644 '{}' "${pkgdir}${MINGW_PREFIX}"/share/'{}' \;
   fi
   if [[ -d "$pkgdir"/share/texmf-dist/scripts ]]; then
      find "${pkgdir}${MINGW_PREFIX}"/share/texmf-dist/scripts -type f -exec chmod a+x '{}' \;
   fi

   msg "Creating Launchers."
   # get the list from 
   # this should be in source.
   # remove comment
   sed -i 's/# This file contains linked scripts list for the package.//' "${srcdir}/${_realname}.scripts"
   # remove useless files
   sed -i /.*readme.*/d "${srcdir}/${_realname}.scripts"

   . "${srcdir}/${_realname}.scripts"
   install -m755 -d "${pkgdir}${MINGW_PREFIX}"/bin
   for _script in ${linked_scripts}; do
      _scriptbase=$(basename $_script)
      _scriptbase=${_scriptbase%.*}
      install -D -m755 ${MINGW_PREFIX}/bin/runscript.exe "${pkgdir}${MINGW_PREFIX}/bin/${_scriptbase}.exe"
   done
}
