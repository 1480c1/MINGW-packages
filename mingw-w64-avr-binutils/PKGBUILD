# Maintainer: fauxpark <fauxpark@gmail.com>

_realname=avr-binutils
pkgbase=mingw-w64-${_realname}
pkgname=${MINGW_PACKAGE_PREFIX}-${_realname}
pkgver=2.35
pkgrel=1
pkgdesc='GNU Binutils for the AVR target (mingw-w64)'
arch=('any')
license=('GPL')
url='https://www.gnu.org/software/binutils/binutils.html'
source=(
    "https://ftp.gnu.org/gnu/binutils/binutils-${pkgver}.tar.xz"
    "01-avr-size.patch"
)
sha256sums=(
    '1b11659fb49e20e18db460d44485f09442c8c56d5df165de9461eb09c8302f85'
    '7aed303887a8541feba008943d0331dc95dd90a309575f81b7a195650e4cba1e'
)

prepare() {
    cd ${srcdir}/binutils-${pkgver}

    mkdir binutils-build

    # https://github.com/archlinux/svntogit-community/blob/packages/avr-binutils/trunk/avr-size.patch
    patch -p1 < ../01-avr-size.patch
}

build() {
    cd ${srcdir}/binutils-${pkgver}/binutils-build

    ../configure \
        --build=${MINGW_CHOST} \
        --prefix=${MINGW_PREFIX} \
        --target=avr \
        --disable-nls \
        --disable-debug \
        --disable-dependency-tracking \
        --disable-werror
    make
}

package() {
    cd ${srcdir}/binutils-${pkgver}/binutils-build

    make DESTDIR="$pkgdir" install

    cd ${pkgdir}${MINGW_PREFIX}

    for info in as bfd binutils gprof ld; do
        mv share/info/${info}.info share/info/avr-${info}.info
    done
}
