# Maintainer: Jeremy Drake <github@jdrake.com>

_realname=fritzing
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=0.9.4
pkgrel=1
pkgdesc="Electronic Design Automation software with a low entry barrier, suited for the needs of makers and hobbyists (mingw-w64)"
arch=('any')
url="https://github.com/fritzing/fritzing-app"
license=('GPL' 'CCPL:cc-by-sa-3.0')
makedepends=("${MINGW_PACKAGE_PREFIX}-boost"
             'git')
depends=("${MINGW_PACKAGE_PREFIX}-qt5"
         "${MINGW_PACKAGE_PREFIX}-libgit2"
         "${MINGW_PACKAGE_PREFIX}-zlib")
source=("${_realname}"::"git+https://github.com/fritzing/fritzing-app.git#tag=CD-498"
        "${_realname}-parts"::"git+https://github.com/fritzing/fritzing-parts.git#branch=master"
        0001-Fix-libgit2-version-check-to-allow-building-with-ver.patch
        0002-Fix-casts-from-pointer-to-long-to-size_t-instead.patch
        0003-qmake-fixes-for-mingw.patch
        system-libgit2.patch)
sha256sums=('SKIP'
            'SKIP'
            '98042c42cc10847f76285e0a84d258d9818571e695dd87dd6d52ef3ac387c7a7'
            '7a91295b4ab91edb4efba9d08a9f2a0cb2761da1b04e39eaca165eee53ac9e6b'
            '2d9f550ce25ddb78ac683d23c55b5b794a06eef3b022f686bad89651793c8423'
            'b9fcdd41207a5d8fd67d44c5d4b3348e25464815431a91b61111b1fd91f1495f')

prepare() {
  cd "${srcdir}"/${_realname}

  git apply "${srcdir}"/0001-Fix-libgit2-version-check-to-allow-building-with-ver.patch
  git apply "${srcdir}"/0002-Fix-casts-from-pointer-to-long-to-size_t-instead.patch
  git apply "${srcdir}"/0003-qmake-fixes-for-mingw.patch
  patch -Np1 -i "${srcdir}"/system-libgit2.patch
}

build() {
  cd "${srcdir}"/${_realname}
  lrelease phoenix.pro
  local qmake_config
  if check_option "debug" "y"; then
    qmake_config=debug
  else
    qmake_config=release
  fi
  MSYS2_ARG_CONV_EXCL="PREFIX=" \
  qmake CONFIG+=${qmake_config} DEFINES+=QUAZIP_LIB PREFIX=${MINGW_PREFIX}
  make
}

package() {
  cd "${srcdir}"/${_realname}

  make INSTALL_ROOT="${pkgdir}" install -j1

  mkdir -p "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/"
  install -Dm644 ${srcdir}/${_realname}/LICENSE.* ${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/

  rm -rf "${pkgdir}${MINGW_PREFIX}/share/fritzing/parts"
  cp -Rf "${srcdir}"/${_realname}-parts "${pkgdir}${MINGW_PREFIX}/share/fritzing/parts"
  cd "${pkgdir}${MINGW_PREFIX}/share/fritzing/parts"
  git remote set-url origin https://github.com/fritzing/fritzing-parts.git
  git branch -M master
  git repack -a -d
  rm -f .git/objects/info/alternates
  "${pkgdir}${MINGW_PREFIX}/bin/Fritzing" -db "${pkgdir}${MINGW_PREFIX}/share/fritzing/parts/parts.db" -pp "${pkgdir}${MINGW_PREFIX}/share/fritzing/parts" -f "${pkgdir}${MINGW_PREFIX}/share/fritzing"
}
