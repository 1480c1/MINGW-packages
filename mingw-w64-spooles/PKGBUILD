# Maintainer: Giuseppe Borzi <gborzi___AT___ieee___DOT___org>
# Contributor: Alberto Penaforte <apenafor@gmail.com>
# Contributor: Rafa≈Ç Brzegowy <rafal.brzegowy@yahoo.com>

_realname=spooles
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=2.2
pkgrel=1
pkgdesc="SParse Object Oriented Linear Equations Solver (mingw-w64)"
arch=('any')
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libgfortran")
makedepends=("${MINGW_PACKAGE_PREFIX}-perl")
url="https://www.netlib.org/linalg/spooles"
license=('custom')
source=("http://www.netlib.org/linalg/${_realname}/${_realname}.${pkgver}.tgz"
	spooles.patch 
	LICENSE)
md5sums=('5789CA60D1AE565A4EAEF6D03CA837AF'
         'D6A8D40DB4F416D1448C18677053CCA8'
	 '37D3B00223FB8E09C680A5C4B957AF1D')
noextract=("${_realname}.${pkgver}.tgz")

prepare() {  
  rm -rf "${srcdir}/${_realname}-${pkgver}"
  mkdir "${srcdir}/${_realname}-${pkgver}"
  cd "${srcdir}/${_realname}-${pkgver}"
  tar -xzf "${srcdir}/${_realname}.${pkgver}.tgz"
  patch -Np1 -i "${srcdir}"/spooles.patch
}

build() {    
  make lib -C "${srcdir}/${_realname}-${pkgver}"
}

package() {
  mkdir -p ${pkgdir}${MINGW_PREFIX}/{lib,include/${_realname}}
  cd "${srcdir}/${_realname}-${pkgver}"
  install -Dm644 lib*.a "${pkgdir}${MINGW_PREFIX}/lib"
  install -Dm644 ./MT/src/lib*.a "${pkgdir}${MINGW_PREFIX}/lib"
  install -Dm644 *.h "${pkgdir}${MINGW_PREFIX}/include/${_realname}"
  install -Dm644 "${srcdir}"/LICENSE ${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE
}
