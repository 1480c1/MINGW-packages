# Maintainer: @naveen521kk on Github, Naveen M K <naveen@syrusdark.website>

_realname=texlive-extra-utils
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=2021.20210430
_revnr=${pkgver#2021.}
pkgrel=1
pkgdesc="TeX Live - TeX auxiliary programs (mingw-w64)"
license=('GPL')
arch=('any')
depends=(
   "${MINGW_PACKAGE_PREFIX}-texlive-core"
)
groups=("${MINGW_PACKAGE_PREFIX}-texlive-most")
url='http://tug.org/texlive/'
source=("${_realname}-${_revnr}.tar.xz::https://github.com/msys2/msys2-texlive/releases/download/${_revnr}/${_realname}-${_revnr}.tar.xz"
        "${_realname}-extra-files.tar.xz::https://github.com/msys2/msys2-texlive/releases/download/${_revnr}/${_realname}-extra-files.tar.xz")
options=('!emptydirs')
sha256sums=('eb070d1c7a1c4c3a6f75fb4c2b8d5b548e9b0c2f6fefe1822b16c68aafde1047'
            '524cc663bf8f930d3e1cea64a1c718426871da3d0db51bf8dda1eeb13029409c')
backup=(
   "${MINGW_PREFIX:1}/etc/texmf/chktex/chktexrc"
)
build() {
   for p in *.tar.xz; do
      bsdtar -xf $p
   done
   rm -rf {tlpkg,doc,source} || true
}

copy_file_message() {
   local from="$1"
   local to="$2"
   msg2 "Copying $from to $to"
   cp $from $to
}


package() {
   msg "Installing Package"

   # Install packages.
   install -m755 -d "${pkgdir}${MINGW_PREFIX}/var/lib/texmf/msys2/installedpkgs"
   sed -i '/^#/d' CONTENTS
   install -m644 CONTENTS "${pkgdir}${MINGW_PREFIX}/var/lib/texmf/msys2/installedpkgs/${pkgname}_${_revnr}.pkgs"
   install -m644 $_realname.maps "${pkgdir}${MINGW_PREFIX}/var/lib/texmf/msys2/installedpkgs/"
   install -m644 $_realname.fmts "${pkgdir}${MINGW_PREFIX}/var/lib/texmf/msys2/installedpkgs/"

   # language files for hooks
   sed -i 's/\% test//' $_realname.dat
   install -m644 $_realname.dat "${pkgdir}${MINGW_PREFIX}/var/lib/texmf/msys2/installedpkgs/"

   sed -i 's/-- test//' $_realname.dat.lua
   install -m644 $_realname.dat.lua "${pkgdir}${MINGW_PREFIX}/var/lib/texmf/msys2/installedpkgs/"

   sed -i 's/\% test//' $_realname.def
   install -m644 $_realname.def "${pkgdir}${MINGW_PREFIX}/var/lib/texmf/msys2/installedpkgs/"

   install -m755 -d "${pkgdir}${MINGW_PREFIX}/share"
   wanteddirs=$(for d in *; do test -d $d && [[ $d != texmf* ]] && echo $d; done) || true
   for dir in $wanteddirs; do
      find $dir -type d -exec install -d -m755 "${pkgdir}${MINGW_PREFIX}"/share/texmf-dist/'{}' \;
      find $dir -type f -exec install -m644 '{}' "${pkgdir}${MINGW_PREFIX}"/share/texmf-dist/'{}' \;
   done

   if [[ -d texmf-dist ]]; then
      find texmf-dist -type d -exec install -d -m755 "${pkgdir}${MINGW_PREFIX}"/share/'{}' \;
      find texmf-dist -type f -exec install -m644 '{}' "${pkgdir}${MINGW_PREFIX}"/share/'{}' \;
   fi
   if [[ -d "$pkgdir"/share/texmf-dist/scripts ]]; then
      find "${pkgdir}${MINGW_PREFIX}"/share/texmf-dist/scripts -type f -exec chmod a+x '{}' \;
   fi
   install -d -m755 "${pkgdir}${MINGW_PREFIX}/etc/texmf/chktex/"
   copy_file_message "${pkgdir}${MINGW_PREFIX}/share/texmf-dist/chktex/chktexrc" \
     	"${pkgdir}${MINGW_PREFIX}/etc/texmf/chktex/"

   msg "Install Scripts"
   #add lauchers:
   _linked_scripts="
authorindex/authorindex
exceltex/exceltex
glossaries/makeglossaries
glossaries/makeglossaries-lite.lua
hyperxmp/hyperxmp-add-bytecount.pl
l3build/l3build.lua
makedtx/makedtx.pl
pax/pdfannotextractor.pl
perltex/perltex.pl
pygmentex/pygmentex.py
splitindex/splitindex.pl
svn-multi/svn-multi.pl
vpe/vpe.pl
webquiz/webquiz.py
wordcount/wordcount.sh
yplan/yplan
"
   install -m755 -d "${pkgdir}${MINGW_PREFIX}"/bin
   for _script in ${_linked_scripts}; do
      _scriptbase=$(basename $_script)
      _scriptbase=${_scriptbase%.*}
      install -D -m755 ${MINGW_PREFIX}/bin/runscript.exe "${pkgdir}${MINGW_PREFIX}/bin/${_scriptbase}.exe"
   done
}
