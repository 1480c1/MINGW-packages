# Maintainer: Stefan Husmann <stefan-husmann@t-online.de>
# Contributor: Dylon Edwards <deltaecho at archlinux dot us>
# Contributor: Dylon Edwards <rafa.brzegowy@yahoo.com>

pkgname='tetgen'
pkgver='1.6.0'
pkgrel=1
pkgdesc='A Quality Tetrahedral Mesh Generator and a 3D Delaunay Triangulator'
arch=('any')
makedepends=("${MINGW_PACKAGE_PREFIX}-ninja")
url='http://wias-berlin.de/software/index.jsp?id=TetGen&lang=1'
license=('AGPL3')
# You must accept the licensing terms before downloading the source archive:
#   http://wias-berlin.de/software/tetgen/download2.jsp
# Place the downloaded archive in $SRCDEST of your /etc/makepkg.conf
source=("local://${pkgname}${pkgver}.tar.gz")
sha256sums=('87b5e61ebd3a471fc4f2cdd7124c2b11dd6639f4feb1f941a5d2f5110d05ce39')
options=('staticlibs')

build() {
  [[ -d "${srcdir}/build-${MINGW_CHOST}" ]] && rm -rf "${srcdir}/build-${MINGW_CHOST}"
  mkdir -p "${srcdir}/build-${MINGW_CHOST}" && cd "${srcdir}/build-${MINGW_CHOST}"

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
    ${MINGW_PREFIX}/bin/cmake \
        -GNinja \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE=Release \
	-DCMAKE_EXE_LINKER_FLAGS="-static" \
	../"${pkgname}${pkgver}"
  ninja
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  install -m 755 -D -t "${pkgdir}${MINGW_PREFIX}/bin" tetgen
  install -m 644 -D -t "${pkgdir}${MINGW_PREFIX}/lib" libtet.a
  install -m 644 -D -t "${pkgdir}${MINGW_PREFIX}/include" ../"${pkgname}${pkgver}/tetgen.h"
}
