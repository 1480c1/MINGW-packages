# Maintainer: Stefan Husmann <stefan-husmann@t-online.de>
# Contributor: Dylon Edwards <deltaecho at archlinux dot us>
# Contributor: Dylon Edwards <rafa.brzegowy@yahoo.com>

pkgname='tetgen'
pkgver='1.6.0'
pkgrel=1
pkgdesc='A Quality Tetrahedral Mesh Generator and a 3D Delaunay Triangulator'
arch=('any')
makedepends=("${MINGW_PACKAGE_PREFIX}-ninja"
             "${MINGW_PACKAGE_PREFIX}-cmake")
url='http://wias-berlin.de/software/index.jsp?id=TetGen&lang=1'
license=('AGPL3')
# You must accept the licensing terms before downloading the source archive:
#   http://wias-berlin.de/software/tetgen/download2.jsp
# Place the downloaded archive in $SRCDEST of your /etc/makepkg.conf
source=(http://www.tetgen.org/1.5/src/${pkgname}${pkgver}.tar.gz)
sha256sums=('87B5E61EBD3A471FC4F2CDD7124C2B11DD6639F4FEB1F941A5D2F5110D05CE39')
options=('staticlibs')

build() {
  [[ -d "${srcdir}/build-${MINGW_CHOST}" ]] && rm -rf "${srcdir}/build-${MINGW_CHOST}"
  mkdir -p "${srcdir}/build-${MINGW_CHOST}" && cd "${srcdir}/build-${MINGW_CHOST}"

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
    ${MINGW_PREFIX}/bin/cmake \
        -GNinja \
        -DCMAKE_BUILD_TYPE=Release \
	-DCMAKE_EXE_LINKER_FLAGS="-static" \
	../"${pkgname}${pkgver}"
  ninja
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  DESTDIR=${pkgdir} cmake --build . --target install
}
