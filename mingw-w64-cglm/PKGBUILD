# Maintainer: Konstantin Podsvirov <konstantin@podsvirov.pro>

_realname=cglm
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=0.7.1
pkgrel=1
pkgdesc="OpenGL Mathematics (glm) for C (mingw-w64)"
arch=('any')
url="https://github.com/recp/cglm"
license=('MIT')
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc"
	"${MINGW_PACKAGE_PREFIX}-python-sphinx"
	"${MINGW_PACKAGE_PREFIX}-python-sphinx_rtd_theme")
options=(!strip staticlibs !buildflags)
source=("${_realname}-$pkgver.tar.gz::https://github.com/recp/cglm/archive/v$pkgver.tar.gz")
sha256sums=('ed8ac01fef1ed4938701e86d8a0744e994975dca01dc20d3fbe419215e459136')

build() {
	cd "${srcdir}/${_realname}-$pkgver"

	sh autogen.sh
	./configure \
		--prefix=${MINGW_PREFIX} \
		--build=${MINGW_CHOST} \
		--host=${MINGW_CHOST}
	make

	cd "${srcdir}/${_realname}-$pkgver/docs"

	sphinx-build source ${_realname}
}

package() {
	cd "${srcdir}/${_realname}-$pkgver"

	make DESTDIR="${pkgdir}/" install
	install -d "${pkgdir}${MINGW_PREFIX}/share/licenses/cglm/"
	install LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/cglm/"
	install -d "${pkgdir}${MINGW_PREFIX}/lib/pkgconfig/"
	install cglm.pc "${pkgdir}${MINGW_PREFIX}/lib/pkgconfig/"

	cd "${srcdir}/${_realname}-$pkgver/docs"

	install -d "${pkgdir}${MINGW_PREFIX}/share/doc/"
	cp -r ${_realname} "${pkgdir}${MINGW_PREFIX}/share/doc/"
}
