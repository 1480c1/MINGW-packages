# Maintainer: fauxpark <fauxpark@gmail.com>

_realname=mdloader
pkgbase=mingw-w64-${_realname}
pkgname=${MINGW_PACKAGE_PREFIX}-${_realname}
pkgver=1.0.4
pkgrel=1
pkgdesc='Massdrop keyboard firmware loader (mingw-w64)'
arch=('any')
license=('GPL3')
url='https://github.com/Massdrop/mdloader'
source=(
    "https://github.com/Massdrop/mdloader/archive/${pkgver}.tar.gz"
    01-get-applet-path.patch
)
sha256sums=(
    '6ead74a393aa538f030ab03dc3a4e0fa18794bad29025e05387a89b45c87a79e'
    'd18bbb1c74dbe6403cb18994b4cf9f3e2635ebb65f594e5e0546acbb2a77d422'
)

prepare() {
    cd ${srcdir}/${_realname}-${pkgver}

    patch -p1 -i ../01-get-applet-path.patch
}

build() {
    cd ${srcdir}/${_realname}-${pkgver}

    make
}

package() {
    cd ${srcdir}/${_realname}-${pkgver}/build

    install -Dm755 mdloader.exe ${pkgdir}${MINGW_PREFIX}/bin/mdloader.exe
    install -Dm644 applet-*.bin -t ${pkgdir}${MINGW_PREFIX}/share/mdloader
}
