_realname=ghdl
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=dev
pkgrel=1
pkgdesc='GHDL: the open-source analyzer, compiler, simulator and (experimental) synthesizer for VHDL (mingw-w64)'
arch=('any')
license=('GPL2+')
url='https://github.com/ghdl'
#checkdepends=("${MINGW_PACKAGE_PREFIX}-python")
makedepends=("${MINGW_PACKAGE_PREFIX}-clang" "${MINGW_PACKAGE_PREFIX}-gcc-ada")
source=("ghdl::git://github.com/ghdl/ghdl.git#commit=e2d751d6")
sha512sums=('SKIP')

build() {
  [[ -d "${srcdir}"/build-${CARCH} ]] && rm -rf "${srcdir}"/build-${CARCH}
  mkdir -p "${srcdir}"/build-${CARCH} && cd "${srcdir}"/build-${CARCH}

  if [ "$CARCH" = "i686" ]; then
    echo 'Configuring ghdl-mcode...'
    ../${_realname}/configure \
        --prefix=${MINGW_PREFIX} \
        --enable-checks \
        --enable-libghdl \
        --enable-synth \
        LDFLAGS="-static"
  fi

  if [ "$CARCH" = "x86_64" ]; then
    echo 'Configuring ghdl-llvm...'
    ../${_realname}/configure \
        --prefix=${MINGW_PREFIX} \
        --enable-checks \
        --enable-libghdl \
        --enable-synth \
        --with-llvm-config="llvm-config --link-static" \
        LDFLAGS="-static"
  fi

  make GNATMAKE="gnatmake -j$(nproc)"
}

# FIXME: Cannot run tests because expected failures make 'check()' exit with error: A failure occurred in check()
#check() {
#  cd $pkgbase-$pkgver/testsuite
#
#  echo 'Running tests for ghdl-mcode...'
#  GHDL="$srcdir"/ghdl-mcode/ghdl_mcode ./testsuite.sh
#
#  echo 'Running tests for ghdl-llvm...'
#  GHDL="$srcdir"/ghdl-llvm/ghdl1-llvm ./testsuite.sh
#}

_package_ghdl-mcode() {
  pkgdesc="$pkgdesc (mcode backend) (mingw-w64)"
  depends=('zlib-devel')

  cd "${srcdir}"/build-${CARCH}
  mkdir -p "${pkgdir}${MINGW_PREFIX}/lib"
  make DESTDIR="${pkgdir}" install

  # License
  install -Dm644 ${srcdir}/${_realname}/doc/licenses.rst ${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/licenses.rst
}

_package_ghdl-llvm() {
  pkgdesc="$pkgdesc (LLVM backend) (mingw-w64)"
  depends=(
    'zlib-devel'
    "${MINGW_PACKAGE_PREFIX}-clang"
    "${MINGW_PACKAGE_PREFIX}-llvm"
  )
  options=(!emptydirs)

  cd "${srcdir}"/build-${CARCH}
  mkdir -p "${pkgdir}${MINGW_PREFIX}/lib"
  make DESTDIR="${pkgdir}" install

  # License
  install -Dm644 ${srcdir}/${_realname}/doc/licenses.rst ${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/licenses.rst
}

package_mingw-w64-i686-ghdl() { _package_ghdl-mcode; }
package_mingw-w64-x86_64-ghdl() { _package_ghdl-llvm; }
