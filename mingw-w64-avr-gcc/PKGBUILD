# Maintainer: fauxpark <fauxpark@gmail.com>

_realname=avr-gcc
pkgbase=mingw-w64-${_realname}
pkgname=${MINGW_PACKAGE_PREFIX}-${_realname}
pkgver=8.4.0
pkgrel=1
pkgdesc='GNU compiler collection for AVR 8-bit and 32-bit microcontrollers (mingw-w64)'
arch=('any')
license=('GPL')
options=(!strip)
url='https://www.gnu.org/software/gcc/gcc.html'
depends=(
    "${MINGW_PACKAGE_PREFIX}-avr-binutils"
    "${MINGW_PACKAGE_PREFIX}-gmp"
    "${MINGW_PACKAGE_PREFIX}-isl"
    "${MINGW_PACKAGE_PREFIX}-mpc"
    "${MINGW_PACKAGE_PREFIX}-mpfr"
)
source=("https://ftp.gnu.org/gnu/gcc/gcc-${pkgver}/gcc-${pkgver}.tar.xz")
sha256sums=('e30a6e52d10e1f27ed55104ad233c30bd1e99cfb5ff98ab022dc941edd1b2dd4')

prepare() {
    cd ${srcdir}/gcc-${pkgver}

    mkdir gcc-build
}

build() {
    cd ${srcdir}/gcc-${pkgver}/gcc-build

    ../configure \
        --build=${MINGW_CHOST} \
        --prefix=${MINGW_PREFIX} \
        --target=avr \
        --enable-languages=c,c++ \
        --disable-nls \
        --disable-libssp \
        --disable-shared \
        --disable-threads \
        --disable-libgomp \
        --disable-libada \
        --with-dwarf2 \
        --enable-mingw-wildcard
    make
}

package() {
    cd ${srcdir}/gcc-${pkgver}/gcc-build

    make DESTDIR="$pkgdir" install

    cd ${pkgdir}${MINGW_PREFIX}

    # strip debug symbols from libraries
    find lib -type f -name "*.a" -exec ${MINGW_PREFIX}/bin/avr-strip --strip-debug '{}' \;

    # info and man7 files conflict with native gcc
    rm -rf share/info share/man/man7
}
